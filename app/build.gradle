import dependencies.Versions
import dependencies.Dependencies

apply {
    plugin("com.android.application")
    plugin("kotlin-android")
    plugin("kotlin-android-extensions")
    plugin("kotlin-kapt")
    plugin("io.gitlab.arturbosch.detekt")
}

android {

    compileSdkVersion Versions.SupportAndroidLibs.compileSdk

    defaultConfig {
        applicationId "com.ahmedadelsaid.moviesampleapp"

        minSdkVersion Versions.SupportAndroidLibs.minSdk
        targetSdkVersion Versions.SupportAndroidLibs.targetSdk

        versionCode Versions.versionCode
        versionName Versions.versionName

        buildConfigField "String", 'API_KEY', API_KEY

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "version"
    productFlavors {
        production {
            dimension "version"
            applicationIdSuffix ".production"
            versionNameSuffix "-production"
            buildConfigField "String", 'BASE_URL', BASE_PRODUCTION_URL
        }
        development {
            dimension "version"
            applicationIdSuffix ".development"
            versionNameSuffix "-development"
            buildConfigField "String", 'BASE_URL', BASE_DEVELOPMENT_URL
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-screens/movielist',
                    'src/main/res-screens/moviedetails'
            ]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

androidExtensions {
    experimental = true
}

detekt {
    toolVersion = "${Versions.detekt}"          // Version of the Detekt CLI that will be used. When unspecified the latest detekt version found will be used. Override to stay on the same version.
    input = files(                              // The directories where detekt looks for input files. Defaults to `files("src/main/java", "src/main/kotlin")`.
            "src/main/java",
            "gensrc/main/java"
    )
    parallel = false                                    // Runs detekt in parallel. Can lead to speedups in larger projects. `false` by default.
    config = files("detekt/default-detekt-config.yml")  // Define the detekt configuration(s) you want to use. Defaults to the default detekt configuration.
    baseline = file("detekt/baseline.xml")              // Specifying a baseline file. All findings stored in this file in subsequent runs of detekt.
    filters = ''                                        // Regular expression of paths that should be excluded separated by `;` or `,`.
    disableDefaultRuleSets = false                      // Disables all default detekt rulesets and will only run detekt with custom rules defined in `plugins`. `false` by default.
    debug = false                                       // Adds debug output during task execution. `false` by default.
    reports {
        xml {
            enabled = true                                  // Enable/Disable XML report (default: true)
            destination = file("detekt/reports/detekt.xml")  // Path where XML report will be stored (default: `build/reports/detekt/detekt.xml`)
        }
        html {
            enabled = true                                  // Enable/Disable HTML report (default: true)
            destination = file("detekt/reports/detekt.html") // Path where HTML report will be stored (default: `build/reports/detekt/detekt.html`)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation Dependencies.Kotlin
    implementation Dependencies.AndroidXLibs
    implementation Dependencies.Google
    implementation Dependencies.Libraries

    kapt Dependencies.Kapt
    kapt Dependencies.Annotations

    implementation Dependencies.Testing
}
